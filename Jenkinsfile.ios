#!/usr/bin/env groovy

def callUnity(Map map = [:]) {
    def project = map.get('project', '.')
    def commands = map.get('commands', '')

    def defaultLogFile = env.STAGE_NAME.replaceAll("\\s", "-").toLowerCase()
    def logFile = map.get('logFile', "logs/${defaultLogFile}.log")

    sh "${env.UNITY_ROOT} -projectPath ${project} -quit -batchmode ${commands} -logFile ${logFile}"
}

def downloadUnitySDK() {
    build job: params.UNITY_SDK_PROJECT_NAME, propagate: true, wait: true
    def unitySDKZip = "*.zip"
    copyArtifacts projectName: "${params.UNITY_SDK_PROJECT_NAME}", selector: lastSuccessful(), filter: unitySDKZip

    def files = findFiles glob: unitySDKZip
    unzip zipFile: files[0].name, dir: env.UNITY_SDK_PATH, quiet: true
    sh "rm -f ${files[0].name}"
}

def writeComboYaml(buildKey) {
    def endpoint = getEndpoint()
    def gameId = params.GAME_ID
    writeFile file: 'combo.yaml', text: """
combo:
  game-id: ${gameId}
  build-key: ${buildKey}
  endpoint: ${endpoint}
  sdk-release-url: https://sdk.seayoo.com/debug
"""
}

// def downloadFrameworks() {
//     def frameworkZip = "*.zip"
//     copyArtifacts projectName: params.IOS_PROJECT_NAME, selector: lastSuccessful(), filter: frameworkZip, excludes: "*dsym*.zip"

//     def files = findFiles glob: frameworkZip
//     unzip zipFile: files[0].name, dir: env.COMBO_SDK_PATH, quiet: true
//     sh "rm -f ${files[0].name}"
// }

def assembleXcodeProject() {
    sh '''
        chmod +x ./combo
        ./combo ios setup --xcode-autoconf --xcode-project build
    '''
}

def updateCodeSigningSettings() {
    sh """
        export LC_ALL=en_US.UTF-8
        export LANG=en_US.UTF-8
        fastlane run update_code_signing_settings path:"${BUILD_DIR}/Unity-iPhone.xcodeproj" code_sign_identity:"${env.CODE_SIGN_IDENTITY}" targets:"Unity-iPhone"
    """
}


def installComboCLI() {
    def credentialsId = getComboCredentialsId('build-key')
    withCredentials([string(variable: 'COMBOSDK_BUILD_KEY', credentialsId: credentialsId)]) {
        writeComboYaml(COMBOSDK_BUILD_KEY)
    }

    def comboZip = "combo-cli_main_darwin_arm64.zip"
    copyArtifacts projectName: 'SDK/Client/Combo/cli/main', filter: comboZip
    unzip zipFile: comboZip, quiet: true
    sh "rm -f ${comboZip}"
}

def getComboCredentialsId(String credentialSuffix) {
    def key = params.GAME_ID
    if (params.GAME_ID != 'demo' && params.TARGET_ENV == 'dev') {
        key = "dev-${params.GAME_ID}"
    }
    return "combo-${key}.${credentialSuffix}"
}

def getCertificate() {
    if (params.GAME_ID == 'demo') {
        return [
            'codesignId': 'combo-demo.codesign-id',
            'developmentTeam': 'combo-demo.development-team'
        ]
    } else {
        return [
            'codesignId': 'combo-dev.codesign-id',
            'developmentTeam': 'combo-dev.development-team'
        ]
    }
}

def loadEnvironmentParams() {
    def certificates = getCertificate() 
    def bundleKeyId = getComboCredentialsId('bundle-id')
    def provisionKeyId = getComboCredentialsId('provision')

    withCredentials([
        string(variable: 'BUNDLE_ID_SECRET', credentialsId: bundleKeyId),
        string(variable: 'PROVISION_SECRET', credentialsId: provisionKeyId),
        string(variable: 'CODE_SIGN_ID_SECRET', credentialsId: certificates.codesignId),
        string(variable: 'DEVELOPMENT_TEAM_SECRET', credentialsId: certificates.developmentTeam)
    ]) {
        env.PRODUCT_BUNDLE_IDENTIFIER = BUNDLE_ID_SECRET
        env.CODE_SIGN_IDENTITY = CODE_SIGN_ID_SECRET
        env.PROVISIONING_PROFILE_SPECIFIER = PROVISION_SECRET
        env.DEVELOPMENT_TEAM = DEVELOPMENT_TEAM_SECRET
    }
}

def getEndpoint() {
    if (params.UI_MODE == "global" && params.TARGET_ENV == "dev") {
        return "https://nv3fcfg4-8091.inc1.devtunnels.ms"
    }
    return (params.TARGET_ENV == "dev") ? "https://api.dev.seayoo.com" : "https://api.seayoo.com"
}

def getDemoEndpoint() {
    return (params.TARGET_ENV == "dev") ? "https://combo-demo.dev.seayoo.com" : "https://combo-demo.seayoo.com"
}

pipeline {
    agent {
        node {
            label 'combo-demo:ios'
        }
    }
    options {
        skipDefaultCheckout()
        disableConcurrentBuilds()
        buildDiscarder(logRotator(
            daysToKeepStr: '15',
            artifactNumToKeepStr: '20'
        ))
        ansiColor('xterm')
        timestamps()
    }
    parameters {
        choice(name: 'GAME_ID', choices: ['demo', 'catsnsoup', 'jxfs'], description: 'GAME_ID')
        choice(name: 'UI_MODE', choices: ['china', 'global'], description: '国内/海外')
        choice(name: 'TARGET_ENV', choices: ['dev', 'prod'], description: '选择线上/测试环境(注意：游戏项目只能在 prod 环境下打包)')
        string(name: 'BUNDLE_VERSION', defaultValue: '1.0.0', description: 'Demo Bundle Version', trim: true)
        choice(name: 'CHECK_UPDATE', choices: ['SKIP_UPDATE', 'HOT_UPDATE', 'FORCE_UPDATE'], description: '模拟游戏的检查更新行为: SKIP_UPDATE-不更新; HOT_UPDATE-热更新; FORCE_UPDATE-强制更新')
        extendedChoice(
            name: 'CAPABILITIES',
            type: 'PT_MULTI_SELECT',
            description: '支持的 Capabilities， 支持多选 (按住 Ctrl 键并单击每一个选项；或者按住 Shift 键并单击要选的第一个选项和要选的最后一个选项以圈定范围)，默认情况下为无',
            value: 'Nothing,SignInWithApple,PushNotifications',
            defaultValue: 'Nothing',
            multiSelectDelimiter: ',',
            quoteValue: false,
            saveJSONParameterToFile: false
        )
        string(name: 'IOS_PROJECT_NAME',
            defaultValue: 'SDK/Client/Combo/ios/main',
            description: 'iOS 工程的 Jenkins Full project name',
            trim: true)
        string(name: 'UNITY_SDK_PROJECT_NAME',
            defaultValue: "SDK/Client/Combo/unity/${env.BRANCH_NAME}",
            description: 'Unity SDK 工程的 Jenkins Full project name',
            trim: true)
        choice(name: 'SKIP_EXPORT', choices: ['NO', 'AUTO', 'YES'], description: '是否跳过 Export Project stage ')
        choice(name: 'SCREEN_ORIENTATION', choices: ['Landscape', 'Portrait'], description: '设置 Demo 横屏幕方向: 横屏/竖屏')
        booleanParam(name: 'CLEAN_WS',
            defaultValue: false,
            description: 'When checked, will clean workspace.')
    }
    environment {
        UNITY_ROOT = getUnityRoot('2019.4.40f1')
        BUILD_DIR = "${env.WORKSPACE}/build"
        COMBO_SDK_PATH = "${env.WORKSPACE}/ComboSDK"
        IOS_IPA = artifactName(name: "combo-demo", extension: "ipa")
        UNITY_SDK_PATH = "${env.WORKSPACE}/Packages"
        CAPABILITIES = params.CAPABILITIES.toString()
        COMBOSDK_GAME_ID = "${params.GAME_ID}"
    }
    stages {
        stage('Clean and Setup') {
            steps {
                script {
                    if (params.CLEAN_WS) {
                        cleanWs()
                    }
                    sh """
                        rm -rf *.ipa
                        rm -rf *.zip
                        rm -rf logs/*
                        rm -rf ${env.COMBO_SDK_PATH}
                        rm -rf ${env.UNITY_SDK_PATH}/com.seayoo.sdk
                    """
                }
            }
        }
        stage('Checkout') {
            steps {
                script {
                    checkout scm
                    sh 'mkdir -p logs'
                }
            }
        }
        stage('Integrate UnitySDK') {
            steps {
                script {
                    downloadUnitySDK()
                }
            }
        }
        stage('Switch Platform') {
            steps {
                script {
                    def platformFile = 'platform.txt'
                    if (fileExists(platformFile)) {
                        echo 'Current platform was already iOS, skip switch.'
                    } else {
                        callUnity  commands: '-buildTarget iOS'
                        writeFile file: platformFile, text: 'iOS'
                    }
                }
            }
        }
        stage('Gen') {
            steps {
                script {
                    loadEnvironmentParams()
                    installComboCLI()
                    // downloadFrameworks()
                    // downloadComboSDKJson()
                }
            }
        }
        stage('Update Settings') {
            environment {
                COMBOSDK_GAME_ID = "${params.GAME_ID}"
                COMBOSDK_ENDPOINT = getEndpoint()
                ENABLE_IOS_POST_BUILD = "false"
            }
            steps {
                script {
                    def publishableKeyId = getComboCredentialsId('publishable-key')
                    withCredentials([
                        string(variable: 'COMBOSDK_PUBLISHABLE_KEY', credentialsId: publishableKeyId)
                    ]) {
                        callUnity commands: "-executeMethod Builder.UpdateComboSDKSettings"
                    }
                }
            }
        }
        stage('Export Project') {
            environment {
                EXPORT_PATH = "build"
                DEMO_ENDPOINT = getDemoEndpoint()
                BUNDLE_VERSION = "${params.BUNDLE_VERSION}"
                CHECK_UPDATE = "${params.CHECK_UPDATE}"
                COMBOSDK_ENDPOINT = getEndpoint()
                SCREEN_ORIENTATION = "${params.SCREEN_ORIENTATION}"
            }
            steps {
                script {
                    def buildKeyId = getComboCredentialsId('build-key')
                    withCredentials([string(variable: 'COMBOSDK_BUILD_KEY', credentialsId: buildKeyId)]) {
                        callUnity commands: "-executeMethod Builder.BuildIOSDemo"
                    }
                }
            }
        }
        stage('Assemble Xcode') {
            steps {
                script {
                    assembleXcodeProject()
                }
            }
        }
        stage('Build IPA') {
            steps {
                script {
                    dir(env.BUILD_DIR) {
                        sh "rm -rf ~/Library/Developer/Xcode/DerivedData"
                        updateCodeSigningSettings()
                        keychainUnlock()

                        sh """xcodebuild clean archive \
                            -project Unity-iPhone.xcodeproj \
                            -scheme Unity-iPhone \
                            -destination generic/platform=iOS \
                            PROVISIONING_PROFILE_SPECIFIER="${env.PROVISIONING_PROFILE_SPECIFIER}" \
                            -archivePath ${params.GAME_ID}.xcarchive
                        """

                        def credentialsId = "combo-${params.GAME_ID}.export-options"
                        withCredentials([file(variable: 'EXPORT_OPTIONS_PLIST', credentialsId: credentialsId)]) {
                            sh """xcodebuild -exportArchive \
                            -archivePath ${params.GAME_ID}.xcarchive \
                            -exportPath ipa \
                            -exportOptionsPlist \${EXPORT_OPTIONS_PLIST}
                            """
                        }
                    }
                }
            }
        }
        stage('Archive') {
            steps {
                script {
                    sh "find ${env.BUILD_DIR}/ipa -name '*.ipa' -exec mv {} ${env.WORKSPACE}/${env.IOS_IPA} \\;"
                    archiveArtifacts artifacts: '*.ipa', onlyIfSuccessful: true
                }
            }
        }
        stage('Upload dSYM') {
            environment {
                DSYM_PATH = "${env.BUILD_DIR}/demo.xcarchive/dSYMs"
            }
            steps {
                withCredentials([string(variable: 'SENTRY_TOKEN', credentialsId: ' sentry-token.seayoo-cn')]) {
                    sh "./combo ios upload-symbols --auth-token $SENTRY_TOKEN --symbols ${DSYM_PATH} --project combo-demo"
                }
            }
        }
    }
    post {
        success {
            script {
                def ipaUrl = "${env.BUILD_URL}artifact/${env.IOS_IPA}"
                def downloads = [
                    ['name': '[ipa]', 'url': ipaUrl]
                ]
                notify withChanges: true, downloads: downloads
            }
        }
        failure {
            notify withChanges: true, withErrors: true
        }
        always {
            script {
                currentBuild.displayName = "${params.GAME_ID} ${params.TARGET_ENV}"
                currentBuild.description = "build_num: ${env.BUILD_NUMBER}"
                zip archive: true, zipFile: 'logs.zip', dir: 'logs'
            }
        }
    }     
}